// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    ext.kotlin_version = "1.3.72"
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/releases' }
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.3'
        classpath "com.diffplug.spotless:spotless-plugin-gradle:3.27.1"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.0"

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    group 'org.eclipse.keyple'
    apply plugin: 'org.jetbrains.dokka'
    apply plugin: "com.diffplug.gradle.spotless"
    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://s01.oss.sonatype.org/content/repositories/snapshots' }
        maven { url 'https://oss.sonatype.org/content/repositories/releases' }
        maven { url 'https://artifactory.coppernic.fr/artifactory/libs-release'}
        google()
        jcenter()
    }

    configurations.all {
        // Should check for updates every build
        resolutionStrategy.cacheChangingModulesFor 0, 'seconds'
    }
}

subprojects{

    dokka {
        outputFormat = 'html'
        outputDirectory = "$buildDir/javadoc"
    }

    spotless {
        kotlin {
            target "**/*.kt"
            ktlint()

            // also supports license headers
            licenseHeader '/********************************************************************************\n' +
                    ' * Copyright (c) $YEAR Calypso Networks Association https://calypsonet.org/\n' +
                    ' *\n' +
                    ' * See the NOTICE file(s) distributed with this work for additional information regarding copyright\n' +
                    ' * ownership.\n' +
                    ' *\n' +
                    ' * This program and the accompanying materials are made available under the terms of the Eclipse\n' +
                    ' * Public License 2.0 which is available at http://www.eclipse.org/legal/epl-2.0\n' +
                    ' *\n' +
                    ' * SPDX-License-Identifier: EPL-2.0\n' +
                    ' ********************************************************************************/'
        }
    }

    task javadoc(type: org.jetbrains.dokka.gradle.DokkaTask) {
        outputFormat = 'javadoc'
        outputDirectory = dokka.outputDirectory
        inputs.dir 'src/main/kotlin'
    }

    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from dokka.outputDirectory
    }

}

ext {
    keyple_version_1_0 = '1.0-SNAPSHOT'
    keyple_version_1_0_plus = '1.0.+'

    keyple_version_2_0 = '2.0-SNAPSHOT'
    keyple_version_2_0_0 = '2.0.0-SNAPSHOT'

    android_nfc_plugin_version = '2.0.0-SNAPSHOT'
    omapi_plugin_version = '2.0.0-SNAPSHOT'
    famoco_plugin_version = '2.0.0-SNAPSHOT'
    coppernic_plugin_version = '2.0.0-SNAPSHOT'
    flowbird_plugin_version = '2.0.0-SNAPSHOT'
    bluebird_plugin_version = '2.0.0-SNAPSHOT'

    appcompat_version = '1.1.0'
    core_ktx_version = '1.3.0'
    coroutines_version = '1.3.3'
    rxjavaVersion = '2.1.13'
    rxandroidVersion = '2.0.2'
    daggerVersion = '2.19'

    slf4j_api_version = '1.7.25'
    timber_version = '4.7.1'
    slf4j_timber_version = '3.1'

    //test
    junit_version = '4.12'
    mockk_version = '1.9'
}