plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

Properties properties = new Properties()
if (project.rootProject.file("local.properties").exists()) {
    properties.load(project.rootProject.file("local.properties").newDataInputStream())
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "org.calypsonet.keyple.demo.control"
        minSdkVersion 21
        multiDexEnabled true
        targetSdkVersion 29
        versionCode 5
        versionName "2021.11"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        if (properties.containsKey("path_to_se_registered_keystore_cna")) {
            releaseCna {
                storeFile file(properties["path_to_se_registered_keystore_cna"])
                storePassword properties["store_password_cna"]
                keyAlias properties["key_alias_cna"]
                keyPassword properties["key_password_cna"]
            }
        }
    }

    buildTypes {
        if (properties.containsKey("path_to_se_registered_keystore_cna")) {
            release {
                signingConfig signingConfigs.releaseCna
            }
        }

        debug {
            debuggable true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
        release {
            debuggable false
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.md'
        exclude 'META-INF/plugin_release.kotlin_module'
    }

    flavorDimensions 'device'
    productFlavors {
        omapi {
            dimension 'device'
            resValue "string", "app_name", "Keyple Control OMAPI"
            applicationIdSuffix ".omapi"
        }
        /*coppernic {
            dimension 'device'
            resValue "string", "app_name", "Keyple Control Coppernic"
            applicationIdSuffix ".coppernic"
        }*/
        famoco {
            dimension 'device'
            resValue "string", "app_name", "Keyple Control Famoco"
            applicationIdSuffix ".famoco"
        }
        mockSam {
            dimension "device"
            resValue "string", "app_name", "Keyple Control Mock Sam"
            applicationIdSuffix ".mockSam"
        }
        bluebird {
            dimension 'device'
            resValue "string", "app_name", "Keyple Control Bluebird"
            applicationIdSuffix ".bluebird"
        }
        flowbird {
            dimension 'device'
            resValue "string", "app_name", "Keyple Control Flowbird"
            applicationIdSuffix ".flowbird"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'

        omapi.java.srcDirs += 'src/omapi/kotlin'
        //coppernic.java.srcDirs += 'src/coppernic/kotlin'
        famoco.java.srcDirs += 'src/famoco/kotlin'
        mockSam.java.srcDirs += 'src/mockSam/kotlin'
        bluebird.java.srcDirs += 'src/bluebird/kotlin'
        flowbird.java.srcDirs += 'src/flowbird/kotlin'
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation 'org.calypsonet.keyple:keyple-demo-common:1.0.0-SNAPSHOT'

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'com.airbnb.android:lottie:3.4.4'

    //Keyple
    implementation "org.eclipse.keyple:keyple-service-java-lib:2.0.0"
    implementation "org.eclipse.keyple:keyple-card-calypso-java-lib:2.0.0"
    implementation "org.eclipse.keyple:keyple-service-resource-java-lib:2.0.0"
    implementation "org.eclipse.keyple:keyple-util-java-lib:2.+"
    implementation "org.eclipse.keyple:keyple-common-java-api:2.0.0-rc4"

    implementation "org.calypsonet.terminal:calypsonet-terminal-reader-java-api:1.0.+"
    implementation "org.calypsonet.terminal:calypsonet-terminal-calypso-java-api:1.0.+"
    implementation "org.calypsonet.terminal:calypsonet-terminal-card-java-api:1.0.+"

    omapiImplementation "org.eclipse.keyple:keyple-plugin-android-omapi-java-lib:2.0.0-rc3"
    omapiImplementation "org.eclipse.keyple:keyple-plugin-android-nfc-java-lib:2.0.0-rc3"

    famocoImplementation "org.eclipse.keyple:keyple-plugin-android-nfc-java-lib:2.0.0-rc3"
    famocoImplementation "org.calypsonet.keyple:keyple-plugin-cna-famoco-se-communication-java-lib:2.0.0-rc1"

    mockSamImplementation "org.eclipse.keyple:keyple-plugin-android-nfc-java-lib:2.0.0-rc3"

    //coppernicImplementation "org.calypsonet.keyple:keyple-plugin-cna-coppernic-cone2-java-lib:2.0.0-rc1"

    bluebirdImplementation "org.calypsonet.keyple:keyple-plugin-cna-bluebird-specific-nfc-java-lib:2.0.0-rc1"

    flowbirdImplementation "org.calypsonet.keyple:keyple-plugin-cna-flowbird-android-java-lib:2.0.0-rc1"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //Multidex
    implementation 'com.android.support:multidex:1.0.3'

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"

    //Joda Time
    implementation "joda-time:joda-time:2.8.1"

    // Google GSON
    implementation "com.google.code.gson:gson:2.8.6"

    // Devnied - Byte Utils
    implementation('com.github.devnied:bit-lib4j:1.4.5') {
        exclude group: 'org.slf4j'
    }

    implementation "org.slf4j:slf4j-api:$slf4j_api_version"
    implementation "com.jakewharton.timber:timber:$timber_version" //Android
    implementation "com.arcao:slf4j-timber:$slf4j_timber_version@aar" //SLF4J binding for Timber


    // Dagger dependencies
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    //Common lang
    implementation 'org.apache.commons:commons-lang3:3.11'

    // Testing dependencies
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
}