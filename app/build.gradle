plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-android-extensions'
    id 'kotlin-kapt'
}

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"

    defaultConfig {
        applicationId "org.calypsonet.keyple.demo.control"
        minSdkVersion 21
        multiDexEnabled true
        targetSdkVersion 29
        versionCode 3
        versionName "2021.07-SNAPSHOT"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }

    packagingOptions {
        exclude 'META-INF/NOTICE.md'
    }

    flavorDimensions 'device'
    productFlavors {
        omapi {
            dimension 'device'
            resValue "string", "app_name", "Keyple OMAPI Control"
            applicationIdSuffix ".omapi"
        }
        coppernic {
            dimension 'device'
            resValue "string", "app_name", "Keyple Control Coppernic"
            applicationIdSuffix ".coppernic"
        }
        famoco {
            dimension 'device'
            resValue "string", "app_name", "Keyple Control Famoco"
            applicationIdSuffix ".famoco"
        }
        mockSam {
            dimension "device"
            resValue "string", "app_name", "Keyple Control Mock Sam"
            applicationIdSuffix ".mockSam"
        }
        flowbird {
            dimension 'device'
            resValue "string", "app_name", "Keyple Flowbird Control"
            applicationIdSuffix ".flowbird"
        }
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
        test.java.srcDirs += 'src/test/kotlin'

        omapi.java.srcDirs += 'src/omapi/kotlin'
        coppernic.java.srcDirs += 'src/coppernic/kotlin'
        famoco.java.srcDirs += 'src/famoco/kotlin'
        mockSam.java.srcDirs += 'src/mockSam/kotlin'
        flowbird.java.srcDirs += 'src/flowbird/kotlin'
    }

    androidExtensions {
        experimental = true
    }
}

dependencies {
    implementation project(":keyple-parser")

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation 'androidx.appcompat:appcompat:1.2.0'
    implementation 'com.google.android.material:material:1.2.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.2'
    implementation 'com.airbnb.android:lottie:3.4.4'

    //Keyple
    implementation "org.eclipse.keyple:keyple-java-core:$keyple_version"
    implementation "org.eclipse.keyple:keyple-java-calypso:$keyple_version"

    omapiImplementation "org.eclipse.keyple:keyple-android-plugin-nfc:$keyple_version"
    omapiImplementation "org.eclipse.keyple:keyple-android-plugin-omapi:$keyple_version"

    famocoImplementation "org.eclipse.keyple:keyple-android-plugin-nfc:$keyple_version"
    famocoImplementation "org.eclipse.keyple:keyple-android-plugin-famoco-se-communication:$famoco_plugin_version"

    mockSamImplementation "org.eclipse.keyple:keyple-android-plugin-nfc:$keyple_version"

    coppernicImplementation "org.eclipse.keyple:keyple-android-plugin-coppernic-ask:$coppernic_plugin_version"

    flowbirdImplementation "org.eclipse.keyple:keyple-android-plugin-flowbird:$flowbird_plugin_version"

    //Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    //Multidex
    implementation 'com.android.support:multidex:1.0.3'

    // RxJava
    implementation "io.reactivex.rxjava2:rxjava:$rxjavaVersion"
    implementation "io.reactivex.rxjava2:rxandroid:$rxandroidVersion"

    //Joda Time
    implementation "joda-time:joda-time:2.8.1"

    //logging
    implementation "org.slf4j:slf4j-api:$slf4j_api_version"
    implementation "com.jakewharton.timber:timber:$timber_version" //Android
    implementation "com.arcao:slf4j-timber:$slf4j_timber_version@aar" //SLF4J binding for Timber

    // Devnied - Byte Utils
    implementation('com.github.devnied:bit-lib4j:1.4.5') {
        exclude group: 'org.slf4j'
    }

    implementation "org.slf4j:slf4j-api:$slf4j_api_version"
    implementation "com.jakewharton.timber:timber:$timber_version" //Android
    implementation "com.arcao:slf4j-timber:$slf4j_timber_version@aar" //SLF4J binding for Timber


    // Dagger dependencies
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-compiler:$daggerVersion"
    kapt "com.google.dagger:dagger-android-processor:$daggerVersion"
    annotationProcessor "com.google.dagger:dagger-android-processor:$daggerVersion"
    implementation "com.google.dagger:dagger:$daggerVersion"
    implementation "com.google.dagger:dagger-android:$daggerVersion"
    implementation "com.google.dagger:dagger-android-support:$daggerVersion"
    compileOnly 'org.glassfish:javax.annotation:10.0-b28'

    //Common lang
    implementation 'org.apache.commons:commons-lang3:3.11'

    // Testing dependencies
    testImplementation 'junit:junit:4.+'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    testImplementation 'org.robolectric:robolectric:4.3.1'
}